
                (role xplayer)
                (role oplayer)

                (base control (xplayer oplayer) )
                (base cell (1 2 3) (1 2 3) (b x o))

                (init (cell 1 1 b))
                (init (cell 1 2 b))
                (init (cell 1 3 b))
                (init (cell 2 1 b))
                (init (cell 2 2 b))
                (init (cell 2 3 b))
                (init (cell 3 1 b))
                (init (cell 3 2 b))
                (init (cell 3 3 b))
                (init (control oplayer))


                (<= (next (cell ?m ?n x))
                    (does xplayer (mark ?m ?n))
                    (true (cell ?m ?n b)))

                (<= (next (cell ?m ?n o))
                    (does oplayer (mark ?m ?n))
                    (true (cell ?m ?n b)))

                (<= (next (cell ?m ?n ?w))
                    (true (cell ?m ?n ?w))
                    (distinct ?w b))

                (<= (next (cell ?m ?n b))
                    (does ?w (mark ?j ?k))
                    (true (cell ?m ?n b))
                    (distinct ?n ?k)
                )

                (<= (next (cell ?m ?n b))
                    (does ?w (mark ?j ?k))
                    (true (cell ?m ?n b))
                    (distinct ?m ?j)
                    (not (distinct ?n ?k))
                )

                (<= (next (control xplayer))
                    (true (control oplayer)))

                (<= (next (control oplayer))
                    (true (control xplayer)))


                (<= (row ?m ?x)
                    (true (cell ?m 1 ?x))
                    (true (cell ?m 2 ?x))
                    (true (cell ?m 3 ?x)))

                (<= (column ?n ?x)
                    (true (cell 1 ?n ?x))
                    (true (cell 2 ?n ?x))
                    (true (cell 3 ?n ?x)))

                (<= (diagonal ?x)
                    (true (cell 1 1 ?x))
                    (true (cell 2 2 ?x))
                    (true (cell 3 3 ?x)))

                (<= (diagonal ?x)
                    (true (cell 1 3 ?x))
                    (true (cell 2 2 ?x))
                    (true (cell 3 1 ?x)))


                (<= (line ?x) (row ?m ?x))
                (<= (line ?x) (column ?m ?x))
                (<= (line ?x) (diagonal ?x))


                (<= (open)
                    (true (cell ?m ?n b)))

                (<= (legal ?w (mark ?x ?y))
                    (true (cell ?x ?y b))
                    (true (control ?w)))

                (<= (legal xplayer noop)
                    (true (control oplayer)))

                (<= (legal oplayer noop)
                    (true (control xplayer)))



                (<= (goal xplayer 100)
                    (line x))

                (<= (goal xplayer 0)
                    (line o))


                (<= (goal oplayer 50)
                    (not (line x))
                    (not (line o))
                    (not (open)))

                (<= (goal xplayer 50)
                    (not (line x))
                    (not (line o))
                    (not (open)))

                (<= (goal oplayer 100)
                    (line o))

                (<= (goal oplayer 0)
                    (line x))

                (<= terminal
                    (line x))

                (<= terminal
                    (line o))

                (<= terminal
                    (not(open)))


                (<= (drawit (1 1 c1xtile))(true(cell 1 1 x)))
                (<= (drawit (1 1 c1otile))(true(cell 1 1 o)))
                (<= (drawit (1 1 c1btile))(true(cell 1 1 b)))

                (<= (drawit (1 2 txtile))(true(cell 1 2 x)))
                (<= (drawit (1 2 totile))(true(cell 1 2 o)))
                (<= (drawit (1 2 tbtile))(true(cell 1 2 b)))

                (<= (drawit (1 3 c2xtile))(true(cell 1 3 x)))
                (<= (drawit (1 3 c2otile))(true(cell 1 3 o)))
                (<= (drawit (1 3 c2btile))(true(cell 1 3 b)))

                (<= (drawit (2 1 lxtile))(true(cell 2 1 x)))
                (<= (drawit (2 1 lotile))(true(cell 2 1 o)))
                (<= (drawit (2 1 lbtile))(true(cell 2 1 b)))

                (<= (drawit (2 2 xtile))(true(cell 2 2 x)))
                (<= (drawit (2 2 otile))(true(cell 2 2 o)))
                (<= (drawit (2 2 btile))(true(cell 2 2 b)))

                (<= (drawit (2 3 rxtile))(true(cell 2 3 x)))
                (<= (drawit (2 3 rotile))(true(cell 2 3 o)))
                (<= (drawit (2 3 rbtile))(true(cell 2 3 b)))

                (<= (drawit (3 1 c4xtile))(true(cell 3 1 x)))
                (<= (drawit (3 1 c4otile))(true(cell 3 1 o)))
                (<= (drawit (3 1 c4btile))(true(cell 3 1 b)))

                (<= (drawit (3 2 dxtile))(true(cell 3 2 x)))
                (<= (drawit (3 2 dotile))(true(cell 3 2 o)))
                (<= (drawit (3 2 dbtile))(true(cell 3 2 b)))

                (<= (drawit (3 3 c3xtile))(true(cell 3 3 x)))
                (<= (drawit (3 3 c3otile))(true(cell 3 3 o)))
                (<= (drawit (3 3 c3btile))(true(cell 3 3 b)))